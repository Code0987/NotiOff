apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.github.triplet.play'

// Keys
def keys_keyAlias = ""
def keys_keyPassword = ""
def keys_storePassword = ""
def keys_serviceAccountEmail = ""
try {
    def keys = new Properties()
    file("$rootDir/../safe/keys.properties").withInputStream { keys.load(it) }

    keys_keyAlias = keys.getProperty("keyAlias", "")
    keys_keyPassword = keys.getProperty("keyPassword", "")
    keys_storePassword = keys.getProperty("storePassword", "")
    keys_serviceAccountEmail = keys.getProperty("serviceAccountEmail", "")
} catch (ex) {
    println(ex)
}

android {
    signingConfigs {
        config {
            keyAlias keys_keyAlias
            keyPassword keys_keyPassword
            storeFile file("$rootDir/../safe/keystore.jks")
            storePassword keys_storePassword
        }
    }
    playAccountConfigs {
        defaultAccountConfig {
            serviceAccountEmail = keys_serviceAccountEmail
            jsonFile = file("$rootDir/../safe/google-play-android-developer.json")
        }
    }
    compileSdkVersion 28
	defaultConfig {
        applicationId "com.ilusons.notioff"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode Integer.parseInt(project.property('versionCode'))
        versionName project.property('version')
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }
    buildTypes {
        debug {
            versionNameSuffix '-debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix '-release'
            signingConfig signingConfigs.config
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.daimajia.swipelayout:library:1.2.0@aar'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.anthonycr.grant:permissions:1.1.2'
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

play {
    track='production'
    userFraction=0.9
    untrackOld=true
    errorOnSizeLimit=true
    uploadImages=false
}
